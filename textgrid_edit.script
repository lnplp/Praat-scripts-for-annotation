#####################################################################################################
##
## This script goes through a directory and for each sound file finds the
## associated textgrid and brings it up for editing. Any changes to the textgrid are saved automatically. 
## The script generates a form on which you can enter the directory and sound file extension. 
## No other settings need editing before running.
##
## Adapted from one of Mietta Lennes' scripts by Leendert Plug; last edit March 2016.
##
######################################################################################################


form Directory and sound file extension
   comment Specify the directory the script should run on by replacing '...' by the full path. Keep the final slash. 
   sentence Directory ...\
   comment Edit the file extension if your sound files are not .wav.
sentence Extension .wav
endform

Create Strings as file list... list 'directory$'*'extension$'
number_of_files = Get number of strings
for x from 1 to number_of_files
     select Strings list
     current_file$ = Get string... x
     Read from file... 'directory$''current_file$'
     object_name$ = selected$ ("Sound")
     Read from file... 'directory$''object_name$'.TextGrid
     plus Sound 'object_name$'
     Edit
     pause  Edit! 
     minus Sound 'object_name$'
     Write to text file... 'directory$''object_name$'.TextGrid
     select all
     minus Strings list
     Remove
endfor

select Strings list
Remove
print Done.